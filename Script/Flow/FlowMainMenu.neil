// License Information:
// **********************************************
// 
// src/Tricky Script/Script/Flow/FlowMainMenu.neil
// (c) Jeroen Broks, 2001, 2018, 2021, 2022, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 22.02.23
// End License Information

#use "Script/Use/Anyway"
#use "libs/linkedlist"

table Nieuw()
	return {}
end

class Stage
	static table Lijst = Nieuw()
	static int Pos = 1
	static var Back = Image.Load("GFX/MainMenu/Ierland.png")
	
	static void Go(int GPos)
		Pos = GPos
		printf("Go: Going to %3d/%3d\n",pos,#Lijst)
		Lijst[Pos].Arrive()
	end
	
	static void Next()	
		Pos = (Pos % #Lijst) + 1
		printf("Next: Going to %3d/%3d",pos,#Lijst)
		Lijst[Pos].Arrive()
	end
	
	static void DrawBack()
		math.random(1,10) // Make sure the seed is entirely messed up. Nobody can press a button twice at exactly the same time
		Back.Stretch(0,0,Screen.W,Screen.H)
	end
	
	void Arrive()
	end
	
	abstract void Cycle()
	
end

class Stage_Logo extends Stage
	
	int Timer = 0
	readonly int MaxTimer = 300
	
	readonly var logo = Image.Load("GFX/Logo/Phantasar Productions.png")
	
	void Cycle()
		Timer = (Timer + 1) % MaxTimer
		if Timer==0
			Stage.Next()
		end
		Stage.DrawBack()
		logo.Draw( (Screen.W div 2) - (logo.Width div 2),  (Screen.H div 2) - (logo.Height div 2) )
	end
end

class Stage_QuickCredits extends Stage
	readonly int MaxPerName = 200
	readonly var Fnt = ImageFont.Obtain("SYSFONT")
	int Timer = 0
	table names
	
	
	get int MPos
		return math.min(#names,(Timer div MaxPerName)+1)
	end
	
	get int TotalMax
		return MaxPerName * #names
	end
	
	CONSTRUCTOR
		names = {}
		names += {"presents",""}
		names += {"A game by:","Jeroen P. Broks"}
		//names += {"",""}
		names += {"Starring",""}
		names += {"W.K. Chan","as Jake Daniel Werrington"}
		names += {"Evelyn Klein-Hulse","as Marrilona, daughter of Fandalora"}
		names += {"Jeroen P. Broks","as Dandor"}
		names += {"Jeroen P. Broks","as Hando Stillor"}
		names += {"",""}
	end
		
	void Cycle()
		__white
		Stage.DrawBack()
		fnt.DarkText(names[mpos][1],Screen.W div 2,(screen.H div 2)-10,2,1)
		fnt.DarkText(names[mpos][2],Screen.W div 2,(screen.H div 2)+10,2,0)
		Timer = (Timer + 1) % TotalMax
		if Timer==0
			Stage.Next()
		end
	end
	
end

class Stage_Title extends Stage
	
	int Timer = 0
	readonly int MaxTimer = 400
	
	readonly var logo = Image.Load("GFX/Logo/Title.png")
	
	void Cycle()
		Timer = (Timer + 1) % MaxTimer
		if Timer==0
			Stage.Next()
		end
		Stage.DrawBack()
		logo.Draw( (Screen.W div 2) - (logo.Width div 2),  (Screen.H div 2) - (logo.Height div 2) )
	end
end

var HS1
class Stage_HandoStillor1 extends Stage
	readonly var Stars
	readonly var Earth
	readonly table Voice
	readonly var YFnt = ImageFont.Load("Font/Calendar.jfbf")
	readonly var Bushes
	int VPos = 0
	var VChan = nil
	int alpha
	
	int EarthScale
	int Year 
	
	get string sYear
		if Year>=0
			return sprintf("%d AD",Year)
		else
			return sprintf("%d BC",math.abs(Year))
		end
	end
	
	get number TrueEarthScale
		return math.max(0.001,EarthScale/500)
	end
	
	void Arrive()
		print("Arrival stuff - HS1")
		EarthScale = 1000
		Year = Lua.tonumber(os.date("%Y")) || 2050
		CSayF("Arrived at stage 1 of Hando Stillor's story! Earth Scale = %d; Year = %d",EarthScale,Year)
		Music.Play("Music/Sys/Silence.ogg")
		//Music.Stop()
		VPos = 1
		VChan = Voice[1].Play()
		alpha = 0
	end
	
	void DStars()
		__white
		Stars.Tile(0,0,Screen.W,Screen.H)
		Graphics.Scale(TrueEarthScale,TrueEarthScale)
		Earth.Draw(Screen.W/2,Screen.H/2)
		Graphics.Scale(1,1)
		YFnt.Text(sYear,Screen.W-100,100,1,0)
		if VPos>4
			EarthScale = math.max(0,EarthScale-1)
		end
		if VPos>5
			Year--
			if Year<2000
				Year--
				Year-=math.random(1,5)
			end
			if Year<-1000
				Year += math.max(0-math.random(1,1000),math.floor(Year*0.75))
			end
		end
	end
	
	void CheckVoice()
		if !Audio.ChannelPlaying(VChan)
			VPos++
			CSayF("Advancing to sample #%2d (of %2d)",VPos,#Voice) 
			if VPos==#voice
				Lua.Neil.Globals.MenuItem.ComeIn=true
			elseif VPos>#Voice
				Music.Play("Music/Hub/StartToFantasyInstrumental.ogg")
				Stage.Next()
				return
			end
			VChan = Voice[VPos].Play()
		end
	end
	
	void DBushes()
		Graphics.SetAlpha(alpha)
		Bushes.Stretch(0,0,Screen.W,Screen.H)
		Graphics.SetAlpha(255)
	end
	
	void Cycle()
		if alpha<255
			DStars()
			if vPos>11
				alpha++
			end
		end
		if alpha>0
			DBushes()
		end
		CheckVoice()
	end
	
	
	Constructor
		Earth  = Image.Load("GFX/Algemeen/Earth.png")
		Stars  = Image.Load("GFX/MainMenu/Starfield.png")
		Bushes = Image.Load("GFX/Algemeen/Bushes.png")
		HS1    = self
		Voice  = {}
		Earth.HotCenter()
		for i=1,25
			Voice[i] = Audio.Load(sprintf("Vocals/General/Introduction/STILLOR_%d.ogg",i))
		end
	End
End

class MenuItem
	static bool ComeIn = false
	static int y
	static var ItemList = new LinkedList()
	static get int Count
		return ItemList.Count
	end
	static get number Scale
		return 1/(Count+1)
	end
	get int iw
		return math.floor(Screen.W*Scale)
	end
	get int x
		return iw*self.ID
	end
	static get int mx
		return Events.MouseX()
	end
	static get int my
		return Events.MouseY()
	end
	get bool hover
		return ComeIn && Y<Screen.H && my>Screen.H-200 && mx>x-(iw div 2) && mx<x+(iw div 2)
	end
	
	readonly int ID
	readonly var Img
	readonly delegate Func
	bool Enabled = true
	readonly string Name
	
	Constructor(string nImg,delegate pFunc)
		ItemList.AddLast(self)
		ID = Count
		Img = Image.Load("GFX/MainMenu/"..nImg..".png")
		Func = pFunc
		Name = nImg
		Img.HotBottomCenter()
		CSayF("Menu item %s; imgtag %s",nImg,Img.Tag)
	end
	
	Destructor
		CSayF("Destroying menu item %s with image tag %s",Name,Img.Tag)
	end
	
	static void NewGame()
		gv.Clear()
		State.LoadFlow("NEWGAME","Script/Flow/FlowNewGame.neil")
		Flow.GotoFlow("NEWGAME")
	end
	
	static void NewGamePlus()
		gv.Clear()
		gv.gbool.NewGamePlus = true
		State.LoadFlow("NEWGAME","Script/Flow/FlowNewGame.neil")
		Flow.GotoFlow("NEWGAME")
	end
	
	static bool AllowNewGamePlus()
		Bool R
		Medals.Game="TFT"
		R = Medals.Awarded("CHAPTER4")
		print("DEBUG! Allow New Game Plus:",R)
		return R
	end
	
	static void Continue()
	end
	
	static Bool IsThereAContinue()
		// TODO: Scan for Quit-Save
		return false
	end
	
	static void LoadGame()
		Loading()
		State.LoadNew("PARTY","Script/Subs/SubParty.Neil")
		State.LoadNew("FLOW_SAVEGAME","Script/Flow/FlowSaveGame.neil")
		State.Call("FLOW_SAVEGAME","WantLoad","mil")
		Flow.Current="SAVEGAME"
	end
	
	static Bool CanLoad()		
		return true
	end
	
	static void Jukebox()
		State.LoadNew("FLOW_JUKEBOX","Script/Flow/FlowJukebox.neil")
		State.Call("FLOW_JUKEBOX","ComeToMe","\"MAINMENU\"")
	end
	
	static void Bugs()
		CSay("Opening your favorite browser to go to the github issue tracker!")
		OpenURL("https://github.com/PhantasarProductions/The-Fairy-Tale-REVAMPED-for-Apollo/issues")
	end
	
	static void ExitGame()
		// This will quit the game as soon as the current cycle is fully finished!") 
		CSay("Use requested to quit")
		Flow.Quit()
	end
	
	static void Create()
		var v
		v = new MenuItem("New Game",NewGame)
		v = new MenuItem("New Game+",NewGamePlus)
		v.Enabled = AllowNewGamePlus()
		v = new MenuItem("Continue",Continue)
		v.Enabled = IsThereAContinue()
		v = new MenuItem("Load Game",LoadGame)
		v.Enabled = CanLoad()
		v = new MenuItem("Jukebox",Jukebox)
		v = new MenuItem("Bugs",Bugs)
		v = new MenuItem("Exit",ExitGame)
		y = Screen.H + (1000*Scale)
	end
	
	void Show()
		__white
		if !Enabled
			Graphics.SetScale(Scale*2,Scale*2)
			Graphics.Color(120,120,120)
			Img.Draw(x,y)
		elseif hover
			Graphics.SetScale(Scale*3,Scale*3)
			Img.Draw(x,y-50)
			if Events.MouseHitLeft()
				Func()
			end
		else
			Graphics.SetScale(Scale*2,Scale*2)
			Img.Draw(x,y)
		end
	end
	
	Static Void ShowAll()
		static var Muis = Image.Obtain("MOUSE")
		if !ComeIn
			return
		end
		if y>Screen.H-20
			y--
			//CSayF("Bringing in the menu. y=%04d",y)
		end		
		for It in ItemList.Each
			It.Show()
		end
		Graphics.SetScale(1,1)
		__white
		Muis.Draw(mx,my)
	End
end



global void Apollo_Flow()	
	static bool StartDestroyed
	if not StartDestroyed
		Flow.Kill("APOLLO_MAIN")
		StartDestroyed = true
	end
	Dev.ToConsole()
	Stage.Lijst[Stage.Pos].Cycle()
	math.random(1,10) // Make sure the seed is entirely messed up. Nobody can press a button twice at exactly the same time
	MenuItem.ComeIn = MenuItem.ComeIn || Events.MouseHitLeft() || Events.KeyHit(13) || Events.KeyHit(32) || Events.KeyHit(27)
	MenuItem.ShowAll()
	
end

Global Void CONSOLE_Credits()
	State.LoadNew("FLOW_CREDITS","Script/Flow/FlowCredits.neil")
	State.Call("FLOW_CREDITS","Reset","\"MAINMENU\"")
	Flow.Current="CREDITS"
End


Global Void CONSOLE_Outtakes()
	State.LoadNew("FLOW_OUTTAKES","Script/Flow/FlowOuttakes.neil")	
	Flow.Current="OUTTAKES"
End


init
	Music.Play("Music/Hub/StartToFantasyInstrumental.ogg")
	Stage.Lijst += new Stage_Logo()
	Stage.Lijst += new Stage_QuicKCredits()
	Stage.Lijst += new Stage_Title()
	// Hando Stillor's intro story requires vocals, so skip it when it's not present
	if GotVocals()
		Stage.Lijst += new Stage_HandoStillor1()
	else
		CSay("No vocals present, so Hando Stillor's intro will NOT be shown!")
	end
	MenuItem.Create()
end