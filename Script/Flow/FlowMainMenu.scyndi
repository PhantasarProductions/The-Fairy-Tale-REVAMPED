// License:
// **********************************************
// 
// src/Code/Script/Flow/FlowMainMenu.scyndi
// (c) Jeroen Broks, 2001, 2018, 2021, 2022, 2026, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 26.01.06 I
// 
// End License

Script

#Use "Libs/SCI/Sys"
#use "Libs/SCI/Audio"
#Use "Libs/SCI/Medals"
#use "Libs/SCI/Events"
#use "Libs/SCI/Graphics"

#use "Libs/Music"
#use "libs/linkedlist"

#use "Script/Use/Link"
#use "Script/Use/Anyway"
#use "Script/Use/Convert"
#use "Script/Use/Misc/PSaveGame"

extern ll "#"

table Nieuw()
	return {}
end

class Stage
	static table Lijst = nil; //Nieuw()
	static int Pos = 1
	static var Back = LoadImage("GFX/MainMenu/Ierland.png","Ierland")
	
	static void Go(int GPos)
		Pos = GPos
		printf("Go: Going to %3d/%3d\n",pos,ll Lijst)
		Lijst[Pos].Arrive()
	end
	
	static void Next()	
		Pos = (Pos % ll Lijst) + 1
		printf("Next: Going to %3d/%3d",pos,ll Lijst)
		Lijst[Pos].Arrive()
	end
	
	static void DrawBack()
		math.random(1,10) // Make sure the seed is entirely messed up. Nobody can press a button twice at exactly the same time
		Back.Stretch(0,0,Graphics.Width,Graphics.Height)
	end
	
	void Arrive()
	end
	
	abstract void Cycle()
	
end

class Stage_Logo extends Stage
	
	int Timer = 0
	readonly int MaxTimer = 300
	
	readonly var logo = LoadImage("GFX/Logo/Phantasar Productions.png","LOGO_PHANPRODUCTIONS")
	
	void Cycle()
		Timer = (Timer + 1) % MaxTimer
		if Timer==0
			Stage.Next()
		end
		Stage.DrawBack()
		logo.Draw( (Graphics.Width div 2) - (logo.Width div 2),  (Graphics.Height div 2) - (logo.Height div 2) )
	end
end

class Stage_QuickCredits extends Stage
	readonly int MaxPerName = 200
	readonly var Fnt = ObtainFont("SYSFONT")
	int Timer = 0
	table names
	
	
	get int MPos
		return math.min(len(names),(Timer div MaxPerName))
	end
	
	get int TotalMax
		return MaxPerName * len(names)
	end
	
	CONSTRUCTOR
		names = {}
		names += {"presents",""}
		names += {"A game by:","Jeroen P. Broks"}
		//names += {"",""}
		names += {"Starring",""}
		names += {"W.K. Chan","as Jake Daniel Werrington"}
		names += {"Evelyn Klein-Hulse","as Marrilona, daughter of Fandalora"}
		names += {"Jeroen P. Broks","as Dandor"}
		names += {"Jeroen P. Broks","as Hando Stillor"}
		names += {"",""}
	end
		
	void Cycle()
		__white
		Stage.DrawBack()
		fnt.DarkText(names[mpos][1],Graphics.Width div 2,(Graphics.Height div 2)-10,2,1)
		fnt.DarkText(names[mpos][2],Graphics.Width div 2,(Graphics.Height div 2)+10,2,0)
		Timer = (Timer + 1) % TotalMax
		if Timer==0
			Stage.Next()
		end
	end
	
end

class Stage_Title extends Stage
	
	int Timer = 0
	readonly int MaxTimer = 400
	
	readonly var logo = LoadImage("GFX/Logo/Title.png","TFT_TITLE")
	
	void Cycle()
		Timer = (Timer + 1) % MaxTimer
		if Timer==0
			Stage.Next()
		end
		Stage.DrawBack()
		logo.Draw( (Graphics.Width div 2) - (logo.Width div 2),  (Graphics.Height div 2) - (logo.Height div 2) )
	end
end

var HS1
class Stage_HandoStillor1 extends Stage
	readonly var Stars
	readonly var Earth
	readonly table Voice
	readonly var YFnt = LinkFont("Font/Calendar.jfbf","Font_Calendar")
	readonly var Bushes
	int VPos = 0
	var VChan = nil
	int alpha
	
	int EarthScale
	int Year 
	
	get string sYear
		if Year>=0
			return sprintf("%d AD",Year)
		else
			return sprintf("%d BC",math.abs(Year))
		end
	end
	
	get number TrueEarthScale
		return math.max(0.001,EarthScale/500)
	end
	
	void Arrive()
		print("Arrival stuff - HS1")
		EarthScale = 1000
		Year = Lua.tonumber(os.date("%Y")) || 2050
		CSayF("Arrived at stage 1 of Hando Stillor's story! Earth Scale = %d; Year = %d",EarthScale,Year)
		Music.Play("Music/Sys/Silence.ogg")
		//Music.Stop()
		VPos = 1
		VChan = Voice[1].Play()
		alpha = 0
	end
	
	void DStars()
		__white
		Stars.Tile(0,0,Graphics.Width,Graphics.Height)
		Graphics.Scale(TrueEarthScale,TrueEarthScale)
		Earth.Draw(Graphics.Width/2,Graphics.Height/2)
		Graphics.Scale(1,1)
		YFnt.Text(sYear,Graphics.Width-100,100,1,0)
		if VPos>4
			EarthScale = math.max(0,EarthScale-1)
		end
		if VPos>5
			Year--
			if Year<2000
				Year--
				Year-=math.random(1,5)
			end
			if Year<-1000
				Year += math.max(0-math.random(1,1000),math.floor(Year*0.75))
			end
		end
	end
	
	void CheckVoice()
		if !Audio.ChannelPlaying(VChan)
			VPos++
			CSayF("Advancing to sample #%2d (of %2d)",VPos,ll Voice) 
			if VPos==ll Voice
				Lua.Neil.Globals.MenuItem.ComeIn=true
			elseif VPos>ll Voice
				Music.Play("Music/Hub/StartToFantasyInstrumental.ogg")
				Stage.Next()
				return
			end
			VChan = Voice[VPos].Play()
		end
	end
	
	void DBushes()
		Graphics.SetAlpha(alpha)
		Bushes.Stretch(0,0,Graphics.Width,Graphics.Height)
		Graphics.SetAlpha(255)
	end
	
	void Cycle()
		if alpha<255
			Self.DStars()
			if vPos>11
				alpha++
			end
		end
		if alpha>0
			Self.DBushes()
		end
		Self.CheckVoice()
	end
	
	
	Constructor
		Earth  = LoadImage("GFX/Algemeen/Earth.png","IEarth")
		Stars  = LoadImage("GFX/MainMenu/Starfield.png","IStars")
		Bushes = LoadImage("GFX/Algemeen/Bushes.png","IBushes")
		HS1    = self
		Voice  = {}
		Earth.HotCenter()
		for i=1,25
			Voice[i] = Audio.Load(sprintf("Vocals/General/Introduction/STILLOR_%d.ogg",i),sprintf("ISN_%d",i))
		end
	End
End

class MenuItem
	static bool ComeIn = false
	static int y
	static var ItemList = new TLinkedList()
	static get int Count
		return ItemList.Count
	end
	static get number Scale
		return 1/(Count+1)
	end
	get int iw
		return math.floor(Graphics.Width*Scale)
	end
	get int x
		return iw*self.ID
	end
	static get int mx
		return Events.MouseX()
	end
	static get int my
		return Events.MouseY()
	end
	get bool hover
		return ComeIn && Y<Graphics.Height && my>Graphics.Height-200 && mx>x-(iw div 2) && mx<x+(iw div 2)
	end
	
	readonly int ID
	readonly var Img
	readonly delegate Func
	bool Enabled = true
	readonly string Name
	
	Constructor(string nImg,delegate pFunc)
		ItemList.AddLast(self)
		ID = Count
		Img = LoadImage("GFX/MainMenu/"..nImg..".png")
		Func = pFunc
		Name = nImg
		Img.HotBottomCenter()
		CSayF("Menu item %s; imgtag %s",nImg,Img.Tag)
	end
	
	Destructor
		CSayF("Destroying menu item %s with image tag %s",Name,Img.Tag)
	end
	
	static void NewGame()
		gv.Clear()
		State.LoadFlow("NEWGAME","Script/Flow/FlowNewGame.neil")
		Flow.GotoFlow("NEWGAME")
	end
	
	static void NewGamePlus()
		gv.Clear()
		gv.gbool.NewGamePlus = true
		State.LoadFlow("NEWGAME","Script/Flow/FlowNewGame.neil")
		Flow.GotoFlow("NEWGAME")
	end
	
	static bool AllowNewGamePlus()
		Bool R
		Medals.Game="TFT"
		R = Medals.Awarded("CHAPTER4")
		print("DEBUG! Allow New Game Plus:",R)
		return R
	end
	
	static void Continue()
		PerformLoadGame("QUIT")
		Flow.Current="FIELD"
	end
	
	static Bool IsThereAContinue()		
		//return SaveGame.Recognized("QUIT")
		return False
	end
	
	static void LoadGame()
		Loading()
		State.LoadNew("PARTY","Script/Subs/SubParty.Neil")
		State.LoadNew("FLOW_SAVEGAME","Script/Flow/FlowSaveGame.neil")
		State.Call("FLOW_SAVEGAME","WantLoad","mil")
		Flow.Current="SAVEGAME"
	end
	
	static Bool CanLoad()		
		return true
	end
	
	static void Jukebox()
		State.LoadNew("FLOW_JUKEBOX","Script/Flow/FlowJukebox.neil")
		State.Call("FLOW_JUKEBOX","ComeToMe","\"MAINMENU\"")
	end
	
	static void Bugs()
		CSay("Opening your favorite browser to go to the github issue tracker!")
		Sys.OpenURL("https://github.com/PhantasarProductions/The-Fairy-Tale-REVAMPED-for-Apollo/issues")
	end
	
	static void ExitGame()
		// This will quit the game as soon as the current cycle is fully finished!") 
		CSay("Use requested to quit")
		Flow.Quit()
	end
	
	static void Create()
		LocExtern s "Scyndi.Class.MenuItem"
		var v
		v = new MenuItem("New Game",s.NewGame)
		v = new MenuItem("New Game+",s.NewGamePlus)
		v.Enabled = s.AllowNewGamePlus()
		v = new MenuItem("Continue",s.Continue)
		v.Enabled = s.IsThereAContinue()
		v = new MenuItem("Load Game",s.LoadGame)
		v.Enabled = s.CanLoad()
		v = new MenuItem("Jukebox",s.Jukebox)
		v = new MenuItem("Bugs",s.Bugs)
		v = new MenuItem("Exit",s.ExitGame)
		y = Graphics.Height + (1000*Scale)
	end
	
	void Show()
		__white
		if !Enabled
			Graphics.SetScale(Scale*2,Scale*2)
			Graphics.Color(120,120,120)
			Img.Draw(x,y)
		elseif hover
			Graphics.SetScale(Scale*3,Scale*3)
			Img.Draw(x,y-50)
			if Events.MouseHitLeft()
				Func()
			end
		else
			Graphics.SetScale(Scale*2,Scale*2)
			Img.Draw(x,y)
		end
	end
	
	Static Void ShowAll()
		static var Muis = ObtainImage("MOUSE")
		if !ComeIn
			return
		end
		if y>Graphics.Height-20
			y--
			//CSayF("Bringing in the menu. y=%04d",y)
		end		
		for It in ItemList.Each
			It.Show()
		end
		Graphics.SetScale(1,1)
		__white
		Muis.Draw(mx,my)
	End
end



global void Apollo_Flow()	
	static bool StartDestroyed
	if !StartDestroyed
		Flow.Kill("APOLLO_MAIN")
		StartDestroyed = true
	end
	//Dev.ToConsole()
	Stage.Lijst[Stage.Pos].Cycle()
	math.random(1,10) // Make sure the seed is entirely messed up. Nobody can press a button twice at exactly the same time
	MenuItem.ComeIn = MenuItem.ComeIn || Events.MouseHitLeft() || Events.KeyHit(13) || Events.KeyHit(32) || Events.KeyHit(27)
	MenuItem.ShowAll()
	
end

Global Void CONSOLE_Credits()
	State.LoadNew("FLOW_CREDITS","Script/Flow/FlowCredits.neil")
	State.Call("FLOW_CREDITS","Reset","\"MAINMENU\"")
	Flow.Current="CREDITS"
End


Global Void CONSOLE_Outtakes()
	State.LoadNew("FLOW_OUTTAKES","Script/Flow/FlowOuttakes.neil")	
	Flow.Current="OUTTAKES"
End


init
	Music.Play("Music/Hub/StartToFantasyInstrumental.ogg")
	Stage.Lijst  = {}
	Stage.Lijst += new Stage_Logo()
	Stage.Lijst += new Stage_QuicKCredits()
	Stage.Lijst += new Stage_Title()
	// Hando Stillor's intro story requires vocals, so skip it when it's not present
	if GotVocals()
		Stage.Lijst += new Stage_HandoStillor1()
	else
		CSay("No vocals present, so Hando Stillor's intro will NOT be shown!")
	end
	MenuItem.Create()
end
