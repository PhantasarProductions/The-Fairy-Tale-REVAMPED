// License Information:
// src/Tricky Script/Script/Flow/FlowCombat/Class_Foe.neil
// Version: 21.09.12
// Copyright (C) 2021 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information

Class Foe Extends Fighter

	Const String ImgDir = "GFX/Combat/Fighters/Foe/"
	
	Var Raw
	Var NorImage
	Var NegImage
	Number Scale = 1
	
	Get Bool TarByFoe
		Return Self.HP>9
	End
	Get Bool TarByHero
		Return Self.HP>9
	End
	
	Get Int W
		Return NorImage.Width * Scale
	End
	
	Get Int H
		Return NorImage.Height * Scale
	End
	
		
	
	Number DeadScale = 1
	Int FlashCount
	
	Bool Oversoul	
	
	Int Num
	Get Int Experience
		if __skill==3 && Oversoul
			Return 0
		End
		Return Raw.Stats.EXP
	End
	String File
	
	Get String Letter
		If num>26
			Return "?"
		End
		Return Chr(num+64)
	End
	
	
	Get String CLetter
		If num>26
			Return "Unknown"
		End
		Return Chr(num+64)
	End

	Get Bool Boss
		Return Raw.Boss==true
	End
	
	
	Static String MakeTag(int _n)
		return sPrintF("FOE%03d",_n)
	End
	
	Static Var Compile(string F,var Rw,int _num=0,_oversoul)
		If _num==0
			Repeat
				_num++
			LoopWhile Self.ByTag[MakeTag(_num)]
		End
		CSayF("Compiling Foe: %s on spot %03d",F,_num)
		Var WFoe = new Foe()
		WFoe.Setup(MakeTag(_num),"Foe")
		WFoe.Num=_num
		WFoe.Oversoul = _oversoul==true
		WFoe.Raw = Rw(WFoe.Oversoul)
		WFoe.NorImage = Image.Load(WFoe.Raw.IMAGE,"NOR"..WFoe.Tag)
		WFoe.NegImage = Image.Load(Replace(WFoe.Raw.IMAGE:upper(),".PNG",".NEGATIVE.PNG"),"NEG"..WFOE.Tag)
		WFoe.NorImage.HotBottomCenter()
		WFoe.NegImage.HotBottomCenter()
		RPGChar.Create(WFoe.Tag)
		WFoe.Char.Name=WFoe.Raw.NAME
		For k,v in Pairs(WFoe.Raw.Stats)
			If k!="EXP"
				string Key                        = self.FURL(k)
				If Key:upper()=="HP" 
					Key="HP"
				End
				WFoe.Char.Stats["BASE_"..Key]     = v
				WFoe.Char.Stats["BUFF_"..Key]     = 0
				WFoe.Char.Stats["END_" ..Key]     = 0
				WFoe.Char.StatScript["END_"..Key] = "SUM:"..WFoe.Tag..".".."BUFF_"..Key..".".."BASE_"..Key
				CSayF("- Stat.%s = { BASE = %d, END = %d }",Key,WFoe.Char.Stats["BASE_"..Key],WFoe.Char.Stats["END_"..Key])
			End
		End
		WFoe.Char.Points.HP.MaxCopy  = "END_HP"
		WFoe.Char.Points.HP.Have     = WFoe.Char.Points.HP.Maximum
		Return WFoe
	End
	
	Void Draw()
		If self.HP==0
			Self.Stage="KO"
		End
		Switch Self.Stage
			Case "IDLE"
				NorImage.ASDraw("COMBAT",Self.X,Self.Y)
			Case "KO"
				Lua.error("Death animation not yet done")
			Default
				Static Bool Neg
				If FPS.Ticks % 25
					Neg = !Neg
					FlashCount = (FlashCount+1) % 5
					If FlashCount==0
						Self.Stage = "IDLE"
					End
				End
				If Neg
					NegImage.ASDraw("COMBAT",Self.X,Self.Y)
				Else
					NorImage.ASDraw("COMBAT",Self.X,Self.Y)
				End
		End
	End
End

Return Foe