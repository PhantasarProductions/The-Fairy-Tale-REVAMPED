// License Information:
// src/Tricky Script/Script/Flow/FlowCombat/Victory.neil
// Version: 22.04.01
// Copyright (C) 2021, 2022 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information
Class Stage_Victory extends Stage

	Static Var Vic
	Static Var VicTune = Audio.LoadNew("Music/Combat/Victory.ogg","VIC_TUNE")
	Static Var Banner  = Image.LoadNew("GFX/Combat/End/YouWin.png","VIC_YOUWIN")
	
	Static Number BanScale
	Const Number ScaleInc = 0.004
	
	Static Void Shout()
		// Vocals/Combat/Hero/Jake/Victory/Boss.ogg
		String Ch		
		Switch LastAction
			case ""
				Return
			Case "Jake_Human" "Jake_Fairy"
				Ch = "Jake"
			Default
				Ch = LastAction
		End
		String FL = sPrintF("Vocals/Combat/Hero/%s/Victory/%s.ogg",Ch,VicQ)
		CSayF("= Shout: %s",FL)
		If JCR6.HasEntry(FL)
			Audio.LoadNew(FL,sprintF("VICTORYQUOTE_%s_%s",Ch,VicQ)).Play()
		End	
		If VicQ=="BOSS"
			For _,d in pairs(Fighter.ByTag)
				If d.Team=="Hero"
					d.Sage = "VICTORY"
				End
			End
		Else
			Fighter.ByTag[LastAction].Stage = "VICTORY"
		End		
	End
		

	Static Void Check()
		If Vic==Stage.Current
			Return
		End
		//Csay("DEBUG CHECK VICTORY!")
		Bool Won = FlowCombat.Defeated("Foe")
		//CSay("Enemy defeated: %s",tostring(Won))
		If Won
			CSay("Party wins, initiating victory stage")
			Stage.Current="VICTORY"
			Banner.HotCenter()
			If Music.Available
				If !Prefixed(FlowCombat.Start.FMusic:upper(),"*NOCHANGE")
					Music.Stop()
					VicTune.Play()
					CSay("= Victory tune")
				End
			End
			BanScale=0
			Shout()
			Master.AllInc("Feenalaria","FeenalariaPoints")
			gv.gint.Combat_Victories++
		End
	End
	
	Void ResetBuffs()
		Static Table ST = {"Power","Endurance","Intelligence","Resistance","Speed","Accuracy","Evasion"}
		For i=1,4
			if RPGParty[i] && RPGParty[i]!=""
				For S in Each(ST)
					Var Ch = RPGChar[RPGParty[i]]
					if Ch.Stat["BUFF_"..s]>0 && __skill>1
						Ch.Stat["BUFF_"..s]=0
					elseif Ch.Stat["BUFF_"..s]<0 && __skill<3
						Ch.Stat["BUFF_"..s]=0
					end
				end
			end
		end
	End

	Void Update()
		BanScale = math.min(1,BanScale+ScaleInc)
		If BanScale>=1
			ResetBuffs()
			Flow.Current="FIELD"
			Ach.Auto()
			If !Prefixed(FlowCombat.Start.FMusic:upper(),"*NOCHANGE")
				Music.Pop()
			End
		End
		plua vit
		plua hp
		String tag
		For i=1,4
			tag=RPGParty[i]
			if tag!=""
				vit = RPGChar[Tag].Points.VIT
				hp  = RPGChar[Tag].Points.HP
				if hp.Have==0
					hp.Have=1
					exprate[tag] = 0
				end
				Int Maxi = hp.Maximum
				If Tag=="Marrilona" && gv.gBool.MarFeena_Curse
					Maxi = gv.gInt.MarFeena_HP
				End
				if vit.Have>0 && hp.Have<Maxi
					hp.Have +=  math.ceil(hp.Maximum/(200/__skill))
					vit.Have = vit.Have - 1
				end
			End
		End
	End

	Void Draw()
		__White
		Graphics.Scale(BanScale,BanScale)
		Banner.Draw(Screen.W div 2, Screen.H div 5)
	End

End

Init
	Stage_Victory.Vic = new Stage_Victory()
	Stage.Register("VICTORY",Stage_Victory.Vic)
End

Return Stage_Victory