// License Information:
// src/Tricky Script/Script/Use/Link.neilbundle/Combat.neil
// Version: 21.12.18
// Copyright (C) 2021 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information
#use "Libs/XMath"

Module

	Const Number PLineInt=0.50
	
	Get Int MidX
		Return Screen.W div 2
	End
	Get Int MidY
		Return Screen.H div 2
	End
	Get Int Radius
		Return math.max(Screen.W,screen.H)
	End
	
	Void Prepare(Int R=0,Int G=180,Int B=255,String BossHead="",String BossName="")
		Number AlphaH=0
		Number AlphaB=0
		Static Var BossAudio
		Static Var BHF = ImageFont.Obtain("BIGTEXT")
		Static Var BBF = ImageFont.Obtain("VBGTEXT")
		Bool BossPlaying()
			If BossHead==""
				Return False
			End
			Return AlphaH<1 && AlphaB<1 && BossAudio & BossAudio.Playing
			//lua.error("No music timing for boss yet!")
		End
		Int Time = 45
		Int m = 360-math.random(0,125)
		If BossHead!=""
			Time = 250
			Music.Push()
			BossAudio = BossAudio || Audio.LoadNew("Audio/Combat/Boss Intro.ogg","BOSS_INTRO")
			Lua.Neil.Globals.Music.Stop()
			BossAudio.Play()			
		End
		
		Repeat		
			Graphics.Cls()
			For li = 0,360,PLineInt
				//number ci = math.abs(xmath.Cos(li+((FPS.Ticks div 10) % 360)))
				number ci = math.abs(xmath.Cos(li+Time+m))
				Graphics.Color(math.floor(R*ci),math.floor(G*ci),math.floor(B*ci))
				Graphics.Line(MidX,MidY,MidX+math.floor(xmath.sin(li)*Radius),MidY+math.floor(xmath.cos(li)*radius))
			End
			Time--
			if BossHead!=""
				Graphics.Color(255,255,255)
				AlphaH = math.min(1,AlphaH+0.1)
				AlphaB = math.min(1,AlphaB+0.009)
				Graphics.SetAlpha(math.floor(AlphaH*255))
				If AlphaH>=1
					BHF.DarkText(BossHead,Screen.W div 2, (Screen.H div 2) - 20 , 2, 1)
				Else
					BHF.Text(BossHead,Screen.W div 2, (Screen.H div 2) - 20 , 2, 1)
				End
				Graphics.SetAlpha(math.floor(AlphaB*255))
				If AlphaB>=1
					BBF.DarkText(BossName,Screen.W div 2, (Screen.H div 2) + 20 , 2, 0)
				Else
					BBF.Text(BossName,Screen.W div 2, (Screen.H div 2) + 20 , 2, 0)
				End
				Graphics.SetAlpha(255)
			End
			Graphics.Flip()
			Events.Poll()
			FPS.WaitMinTicks()
		LoopWhile Time>0 || BossPlaying()
		Lua.Neil.Globals.Loading()
		State.LoadNew("FLOW_COMBAT","Script/Flow/FlowCombat.neil")
		If BossHead!=""
			ISSet("BOOL","FLOW_COMBAT","FlowCombat.Start.Music_DontPush",true)
		End
		State.Call("FLOW_COMBAT","FlowCombat.Start.StartPreparation","nil")
	End
	
	Void Boss(String BossHead, String BossName,Int R=255,Int G=0, Int B=0)
		Prepare(R,G,B,BossHead,BossName)
		ISSet("String","FLOW_COMBAT","FLowCombat.Start.Lose","GameOver")
	End
	
	Void Start()
		State.Call("FLOW_COMBAT","FlowCombat.Start.Start","nil")
	End
	
	Set String NewEnemy
		State.Call("FLOW_COMBAT","FlowCombat.Start.AddFoe",sprintf("'%s'",value))
	End
	
	// Last Enemy X
	Set Int LEX
		ISSet("Int","FLOW_COMBAT","FLowCombat.Start.LastFoe.X",value)
	End
	
	// Last Enemy Y
	Set Int LEY
		ISSet("Int","FLOW_COMBAT","FlowCombat.Start.LastFoe.Y",value)
	End
	
	Set String Arena
		ISSet("String","FLOW_COMBAT","FLowCombat.Start.Arena",value)
	End
	
	Set String Lose
		ISSet("String","FLOW_COMBAT","FLowCombat.Start.Lose",value)
	End
	
	Set String Music
		ISSet("String","FLOW_COMBAT","FLowCombat.Start.FMusic",value)
	End
	
	Set String StartEvent
		ISSet("String","FLOW_COMBAT","FLowCombat.Start.StartEvent",value)
	End
	
	Void QuickBoss(string Header,String BossName,String Boss1,string Boss2="",string Boss3="",string Boss4="")
		
		Boss(Header,BossName)
		Arena = Lua.Neil.Globals.Map.Arena
		Music = "*BOSS"
		Lose = "GameOver"
		For Boss in Each{Boss1,Boss2,Boss3,Boss4}
			If Boss!=""
				NewEnemy = "Boss/"..Boss
			End
		End
		Lua.Neil.Globals.Map.StopWalking()
		Start()
	End


End