// License Information:
// src/Tricky Script/Script/Subs/SubItems.neil
// Version: 21.12.29
// Copyright (C) 2021 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information

#use "Script/Use/Anyway.neilbundle/Dev"
#use "Script/Data/IAA/MyData_ClassFile_IAA"
#use "Libs/Fittext"

#macro __ScrollY ScrollList[Filters]
#macro __ScrollAY ScrollList[Situation]

ReadOnly Var FilFnt = ImageFont.Obtain("FNTSBAR")
ReadOnly Var ItmFnt = Imagefont.Obtain("FNTSTATSTAT")
Global ReadOnly Int MaxItem=({100,50,10})[gv.gint.skill]

Table LoadedItems = {}

Global Table Inventory = {["ITM_APPLE"]=24 div gv.gInt.Skill, ["ITM_ANTIDOTE"]=6 div gv.gint.skill,["ITM_HEALINGHERB"]=3-gv.gint.skill }
Global Table Abilities = {}

Class ABLRec
	ReadOnly String iTag
	ReadOnly String Char
	Get Var Ability
		LoadedItems[iTag] = LoadedItems[iTag] || NeilUse("Script/Data/IAA/"..iTag)
		Return LoadedItems[iTag]
	End
	
	Get String UnlTag
		string c=Char
		if prefixed(c,"Jake") 
			c="jake"
		End
		Return sprintf("%s.%s",c,itag)
	End
	//Bool _Unlocked
	Get Bool Unlocked
		if Abilities[Char].Skill[iTag]
			Return True
		End
		Return gv.gBool[UnlTag]		
	End
	Set Bool Unlocked
		gv.gBool[UnlTag]=value
	End
	
	ReadOnly Int Req1
	ReadOnly Int Req2
	ReadOnly Int Req3
	ReadOnly Int Req4
	ReadOnly Int Req5
	ReadOnly String Category
	Get Bool ReqByStory
		Return Req1==0 && Req2==0 && Req3==0 && Req4==0 && Req5==0 && (!Abilities[Char].Skill[iTag])
	End
	
	Get Int APCost
		If Char=="Marrilona" && (RPGChar.Marrilona.Points.HP.Have/RPGChar.Marrilona.Points.HP.Maximum)<(0.30/gv.gint.skill)
			Return 0
		End
		Return Ability.ABL_APCost
	End
	
	Get String ShowAPCost
		If Unlocked
			return sprintf("%d",APCost)
		End
		Return ""
	End
	
	
	Get String ShowName
		If Unlocked
			Return Ability.Title
		End
		String Ret = ""		
		For i=1,#Ability.Title
			If Mid(Ability.Title,i,1)==" "
				Ret += " " 
			Else
				Ret += "-"
			End
		End
		Return Ret
	End
	
	Constructor(string T,string CH,int R1, int R2, int R3, int R4, int R5,string C)
		iTag     = T:upper()
		Char     = Ch
		Category = C
		Req1     = R1
		Req2     = R2
		Req3     = R3
		Req4     = R4
		Req5     = R5
	End
	
	Static Void NewAbl(string T,string Ch,int R1, int R2, int R3, int R4, int R5,string Cat)
		T = T:upper()
		Abilities[ch] = Abilities[ch] || { ["Ability"]={}, ["Skill"]={} }		
		Abilities[ch].Ability[T] = new ABLRec(T,Ch,R1,R2,R3,R4,R5,Cat)
		If ch=="Jake"
			Abilities.Jake_Human = Abilities.Jake_Human || { ["Ability"]={}, ["Skill"]={}}
			Abilities.Jake_Fairy = Abilities.Jake_Fairy || { ["Ability"]={}, ["Skill"]=Abilities.Jake_Human.Skill }
			Abilities.Jake_Human.Ability[T] = Abilities[ch].Ability[T]
			Abilities.Jake_Fairy.Ability[T] = Abilities[ch].Ability[T]
		ElseIf Prefixed(ch,"Feenalaria")
			// Feenalaria copies Jake's skills
			Static Bool FirstTime = False
			If FirstTime
				Abilities.Jake_Human = Abilities.Jake_Human || { ["Ability"]={}, ["Skill"]={} }
				Abilities[ch].Skill = Abilities.Jake_Human.Skill
				FirstTime = False
			End
		End
	End
	
	Static Void NewSkill(string T, string CH)
		T = T:upper()
		If Prefixed(ch,"Jake")
			Abilities.Jake_Human = Abilities.Jake_Human || { ["Ability"]={}, ["Skill"]={} }
			Abilities.Jake_Fairy = Abilities.Jake_Fairy || { ["Ability"]={}, ["Skill"]=Abilities.Jake_Human.Skill }
		Else
			Abilities[ch] = Abilities[ch] || { ["Ability"]={}, ["Skill"]={} }
		End
		// Conflict prevention
		If Abilities[ch].Skill[T]
			CSayF("Conflict on new skill for %s (%s)",ch,T)
			Return
		End
		Abilities[ch].Skill[T] = new ABLRec(T,ch,0,0,0,0,0,"9. Skills")
		CSayF("Skill %s added to %s as %s (Skill list: %s)",T,ch,Lua.tostring(Abilities[ch].Skill[T]),Lua.tostring(Abilities[ch].Skill))
	End
	
	Static Void CopyHumanJake(string Ch,string LinkSkill,Force)
		If (!Abilities[ch]) || Force
			Abilities[ch] = { ["Ability"]={}, ["Skill"]=Abilities[LinkSkill].Skill }
			For k,v in pairs(Abilities.Jake_Human)
				If v.Req1>0 && v.Reg2<=0 && v.Reg3<=0 && v.Reg4<=0 && v.Reg5<=0 && v.Unlocked
					PrintF("ITEM>".."%s copied ability %s from Jake",Ch,k)
					Abilities[ch][k] = v
				End
			End
		End
	End
	
	Static Void CopyMarrilona(string Ch,Force)
		Static Bool DoneAlready = False
		If (!DoneAlready) || Force
			For k,v in pairs(Abilities.Marrilona)
				PrintF("ITEM>".."%s copied ability %s from Marrilona",Ch,k)
				Abilities[ch][k] = v
			End
		End
	End
			
End

Global Void SaveInventory()
	String Data = "Init\n// Inventory\n"
	For k,v in sPairs(Inventory)
		Data += sPrintf("\tInventory[\"%s\"] = %4d\n",k,v)
	End
	Data += "\n\n//Skills\n"
	For ch,abl in spairs(Abilities)
		// Prevent dupes on Jake
		If Ch!="Jake_Fairy"
			For tag,_ in spairs(Abl.Skill)
				Data += sprintf("\tAblRec.NewSkill(\"%s\",\"%s\")\n",tag,ch)
			End
		End
	End
	Data += "End\n\n"
	SaveGame.Data["INVENTORY.NEIL"] = Data
End

Global Void LoadInventory()
	If SaveGame.HasData("INVENTORY.NEIL")
		NeilDoString(SaveGame.Data["INVENTORY.NEIL"])
	End
End

Global Void SetItemHas(string itTag,int num)
	Print("SetItemHas",itTag,num)
	Inventory[itTag:upper()] = math.max(math.min(num,maxitem),0)
End

Global Bool Allow(string itTag, string Filter,string Char="")
	itTag = itTag:upper()
	Filter = Filter:upper()
	LoadedItems[itTag] = LoadedItems[itTag] || NeilUse("Script/Data/IAA/"..ItTag)
	Var MyItem = LoadedItems[itTag]
	Static String EQP="EQUIPMENT FOR "
	EQP = Replace(EQP,"HANDO STILLOR","HANDOSTILLOR")
	If Prefixed(Filter,EQP)
		String Name = Right(Filter,#Filter-#EQP)
		Return MyItem.ITM_EQPFOR:upper()==Name || MyItem["ITM_ACC_"..Name]
	End
	Switch Filter
		Case "ALL" "" "*"
			Return True
		Case "EQUIPMENT"
			Return MyItem.ITM_Type=="Weapon" || MyItem.ITM_Type=="Armor" || MyItem.ITM_Type=="Accesoiry"
		Case "CONSUMABLE" "COMBAT" "CONSUMABLES"
			Return MyItem.ITM_Type=="Consumable"
		Case "KEY" "KEYITEM" "KEY ITEM"
			Return MyItem.ITM_Type=="KeyItem"
		Case "SELLABLE"
			Return (MyItem.ITM_Sellable || MyItem.ITM_ShopPrice>0) && MyItem.ITM_Type!="KeyItem"
		Default			
			If Prefixed(Filter,"FOR")
				If (MyItem.ITM_Type=="Weapon" || MyItem.ITM_Type=="Armor")
					Return MyItem.ITM_EQP_For==Char
				ElseIf MyItem.ITM_Type=="Accesoiry"
					Return MyItem["ITM_ACC_"..Char]
				End
			Else
				Lua.error("Unknown item filter: "..Filter)
			End
	End
End

Global Var ItemField(string ItTag,ItField)
	ItTag = ItTag:upper()
	ItField = ItField:upper()
	Switch ItField
		Case "HAS" "NUMBER" "AMOUNT" "HAVE" 
			Return Inventory[itTag] || 0
		Default
			LoadedItems[itTag] = LoadedItems[itTag] || NeilUse("Script/Data/IAA/"..ItTag)
			//cOut("Item requested: ",ittag,"; Field: ",itfield," => ",LoadedItems[itTag][ItField])
			Return LoadedItems[itTag][ItField]
	End
End

Global Void Explain(it)
	if (Lua.type(it)=="string")
		// By lack of a proper overload I guess this will have to do
		LoadedItems[it] = LoadedItems[it] || NeilUse("Script/Data/IAA/"..It)
		Explain(LoadedItems[it])
		return
	End
	Int MX = Events.MouseX()
	Int MY = Events.MouseY()
	Graphics.Color(255,255,255)
	//FitText.Dark(FilFnt,"DEBUG FUCK!",MX+10,MY+100)
	FitText.Dark(FilFnt,it.Desc,MX+10,MY+100)
End

Global Int GetSellPrice(string iTag)
	LoadedItems[iTag] = LoadedItems[iTag] || NeilUse("Script/Data/IAA/"..iTag)
	var s = LoadedItems[iTag]
	if !s.ITM_Sellable
		Return 0
	End
	Switch gv.gint.Skill
		case 1
			Return s.ITM_ShopPrice
		case 2
			Return math.floor(s.ITM_ShopPrice * 0.75)
		case 3
			Return math.floor(s.ITM_ShopPrice * 0.25)
		default
			return 0
	End
	// Only to make 100% sure something is returned, but should never be needed!
	Return 0
End

Global String OverView(int x, int y, int w, int h, string Filters,string char)
	// Basis
	Static Table ListOfFilters = { ["FIELDMENU"] = {"All","Consumables","Combat","Equipment","For <ch>","Key"}, ["COMBAT"] = {"Combat"}, ["STORE"] = {"Sellable"}, ["SHOP"]={"All"}, ["WSHOP"]={"All"} }
	Static Table CurrentFilter = {}
	Static Table ScrollList = {}
	Int MX = Events.MouseX()
	Int MY = Events.MouseY()
	Bool ML = Events.MouseHitLeft()
	ListOfFilters.FIELD = ListOfFilters.FIELDMENU
	Lua.assert(ListOfFilters[Filters],"No filterlist for situation: "..Filters)
	CurrentFilter[Filters] = CurrentFilter[Filters] || 1
	String CF = ListOfFilters[Filters][CurrentFilter[Filters]]
	__ScrollY = __ScrollY || 0
	// Filter menu
	Int FW = w div (#ListOfFilters[Filters]+1)
	For i,fitem in iPairs(ListOfFilters[Filters])
		String CFI = fitem
		Switch Char
			Case "Jake_Human" "Jake_Fairy"
				CFI = Replace(CFI,"<ch>","Jake")
			Case "Marrilona" "Dandor" 
				CFI = Replace(CFI,"<ch>",Char)
			Case "HandoStillor"
				CFI = Replace(CFI,"<ch>","Hando Stillor")
			Default
				If Suffixed(CFI,"<ch>")
					CFI=""
				End
		End
		Int TW = FilFnt.TextWidth(CFI)
		Int WH = TW div 2
		Int FX = (i * FW)+x
		if i==CurrentFilter[Filters]
			Graphics.Color(50,36,0)
			Graphics.Rect(FX-WH,y+5,TW,40)
			Graphics.Color(255,180,0)
			Graphics.Rect(FX-WH,y+5,TW,40,true)
		End
		Graphics.Color(255,180,0)
		If CFI!="" && MY>Y && MY<Y+50 && MX>FX-WH && MX<FX+WH
			Graphics.Color(255,255,0)
			If ML
				CurrentFilter[Filters] = i
			End
		End
		FilFnt.DarkText(CFI,FX,y+25,2,2)
	End
	// Items
	Int iY = -__ScrollY
	//String Hover = ""
	For iTag,iNum in sPairs(Inventory)
		LoadedItems[iTag] = LoadedItems[iTag] || NeilUse("Script/Data/IAA/"..iTag)
		Var iData = LoadedItems[iTag]
		
		If iY>=0 && iY<=H-150 && iNum>0 && Allow(iTag,ListOfFilters[Filters][CurrentFilter[Filters]],Char)
			Int MDX = 0
			Int DX = X+10
			Int DY = Y+iY+100
			bool Hovering = False
			If MX>DX && MX<(X+W)-80 && MY>DY && MY<DY+35
				MDX = 30
				Hovering = True
				//Hover = iTag
				If ML
					Return iTag
				End
			End
			Graphics.Color(255,255,255)
			ItmFnt.Text(iData.Title,DX+MDX,DY)
			Graphics.Color(255,180,  0)
			ItmFnt.Text(Lua.tostring(iNum),(X+W)-(80+MDX),DY,1)
			if Filters=="SHOP" || Filters=="WSHOP"
				Graphics.Color(0,180,255)
				ItmFnt.Text(Lua.tostring(GetSellPrice(iTag)).." Sh",(X+W)-(200+MDX),DY,1)
			end
			iY+=40
			If Hovering
				Explain(iData)
			End
		End
	End
	Return ""
End

Void Categorize(String Char)
	If !Abilities[char].Cat
		Abilities[char].Cat = {}
		Table Q = {["9. Skills"]=true}
		For _,A in pairs(Abilities[char].Ability)
			Q[A.Category] = true
		End
		For C,_ in sPairs(Q)
			Abilities[char].Cat += C
		End
		PrintF("Abilities for %s categorized!\n",Char)
	End
End
		
Global Void QuickWork(Table AT,String Char)
	Printf("Abilities for: %s\n",char)
	For Category,Lijst in Pairs(AT)
		printF("- Category: %s\n",Category) 
		For AbTag,Req in Pairs(Lijst)
			printF(" = %s => %d/%d/%d/%d/%d\n",Abtag, Req[1] || 0,Req[2] || 0,Req[3] || 0,Req[4] || 0,Req[5] || 0)
			AblRec.NewAbl(AbTag,Char,Req[1] || 0,Req[2] || 0,Req[3] || 0,Req[4] || 0,Req[5] || 0,Category)
		End
	End
End

Global Void LoadAbilityList(String Char,Force)
	If (!Abilities[char]) || Force
		NeilUse("Script/Data/Char/"..char.."/Abilities")
	End
	If Char=="Jake_Human"
		LoadAbilityList("Jake_Fairy",true)		
	End
	Categorize(Char)
End

Global String AblCheckLearn(String Char)
	If (!Abilities[char])
		NeilUse("Script/Data/Char/"..char.."/Abilities")
	End
	For tag,abl in pairs(Abilities[Char].Ability)
		Print("ABLCHECKLEARN> ",type(abl),tag)
		Bool ReqOk=!abl.Unlocked
		Bool AllZero = True
		For si=1,5 			
			If Abl["Req"..si]>0
				ReqOk = ReqOk && (!abl.Unlocked) && RPGChar[Char].Points["LVL_Skill"..si].Have>=Abl["Req"..si]
				AllZero=False
			End
		End		
		if ReqOk && (!AllZero)
			abl.Unlocked=true
			Return tag
		End
	End
	Return ""
End



Global String AblOverview(String Char,int x, int y, int w, int h,string situation)
	Static Table ScrollList = {}
	Static Table CurrentCat = {}
	String Ret = "" 
	Int MX = Events.MouseX()
	Int MY = Events.MouseY()
	Bool ML = Events.MouseHitLeft()
	
	LoadAbilityList(Char)
	CurrentCat[char] = CurrentCat[char] || 1
	__ScrollAY = __ScrollAY || 0
	Int cw = w div (#Abilities[Char].Cat+1)
	String ctn = Abilities[Char].Cat[CurrentCat[Char]]
	For i,ct in ipairs(Abilities[Char].Cat)
		Int CX = i*cw 
		Int CW = FilFnt.TextWidth(ct)
		Int WH = CW div 2
		Graphics.Color( 50, 36,0)
		Graphics.Rect(x+cx-wh,y+5,cw+2,45)
		Graphics.Color(255,180,0)
		If MX>x+cx-wh && MX<x+cx+wh && MY>y && MY<50+y
			Graphics.Color(255,255,0)
			If ML 
				CurrentCat[char]=i
				Print("Category set to: ",i)
			End
		End
		If i==CurrentCat[char]
			Graphics.Rect(x+cx-wh,y+5,cw+2,45,true)
		End
		FilFnt.DarkText(ct,x+CX,y+25,2,2)
	End
	Int AY = y+75	
	Var ALs = Abilities[Char].Ability
	If CurrentCat[char]==#Abilities[Char].Cat
		Als = Abilities[Char].Skill
		//Lua.error("Skill!") // For debug use ONLY!
	End	
	//CSayF("Listing char %s category %s(%s)",Char,CTN,lua.tostring(Als))
	For Abl,Dat In sPairs(Als)
		Int DX = 0
		Bool Hover
		//CSayF("Check '%s'/'%s' (%s)",Dat.Category,CTN,Lua.tostring(Als)) 
		If Dat.Category==CTN
			If MX>X && MY>AY && MX<X+W-80 && MY<AY+40
				DX = 20
				Hover=true
				If ML && Dat.Unlocked
					Ret = Abl
				End				
			End
			Graphics.Color(255,255,255)
			ItmFnt.DarkText(Dat.ShowName,X+DX+5,AY)
			Graphics.Color(  0,180,255)
			ItmFnt.DarkText(Dat.ShowAPCost,X+W-(80+DX),AY)
			If Hover
				If Dat.Unlocked
					Explain(Abl)
				Else
					Int EY = MY+100
					If Dat.ReqByStory 
						FitText.Dark(FilFnt,"Unlocked when you reach a certain point in the scenario",MX+10,EY)
					Else
						FitText.Dark(FilFnt,"To unlock you need:",MX+10,EY)
						For si=1,5 
							If Dat["Req"..si]>0
								EY+=20
								If RPGChar[char].Points[sprintf("LVL_Skill%d",si)].Have==0
									FitText.Dark(FilFnt,"???",MX+10,EY)
								Else
									FitText.Dark(FilFnt,sPrintF("%s to be level %d or higher",RPGChar[char].Data[sprintf("Skill%d",si)],Dat[sprintf("Req%d",si)]),MX+10,EY)
								End
							End
						End
					End
				End
			End
			AY+=40
		End
	End
	Return Ret
End


Global Void UnlockJakeShift()
	LoadAbilityList("Jake_Fairy")
	LoadAbilityList("Jake_Human")
	Abilities.Jake_Fairy.Ability.ABL_HERO_JAKE_SHIFT_FAIRY.Unlocked = true
	Abilities.Jake_Human.Ability.ABL_HERO_JAKE_SHIFT_HUMAN.Unlocked = true
End