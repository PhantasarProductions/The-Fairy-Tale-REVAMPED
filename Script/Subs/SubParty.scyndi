// License:
// **********************************************
// 
// src/Code/Script/Subs/SubParty.scyndi
// (c) Jeroen Broks, 2021, 2022, 2026, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 26.01.21 VIII
// 
// End License


Script

#use "Libs/SCI/SString"
#use "Libs/SCI/Graphics"

#Use "Script/Use/Link"
#Use "Script/Use/Anyway"
#use "Script/Use/Convert"
#Use "Script/Use/Misc/Box_Class"
#Use "Libs/XMath"

#macro __BoxPosY Screen.H-100

Table SubStat = {[0]="BASE","BUFF","PERMABUFF","EQUIP","END","MASTER"}
Table Stats = {[0]="Power","Endurance","Intelligence","Resistance","Speed","Accuracy","Evasion","Critical","HP","AP"}
Table Elements = {[0]="Flame","Wind","Water","Earth","Lightning","Frost","Light","Darkness"}
Table Ailments = {[0]="Poison","Disease","Paralysis","Curse","Silence","Sleep","Death"}



Group SubParty

	Table Pictures = {}
	
	ReadOnly Var Fnt = ObtainFont("FNTSBAR") 
	ReadOnly Var BFnt = ObtainFont("FNTSTATSTAT")

	Void GrabLevel(string char,Int Level, noauto)
		String n := char
		//string dat = JCR6.LoadString(sprintf("Data/LvStats/%s/%d",n,math.floor(level/100)))
		String dat := JCR6.LoadString(sPrintF("Data/CharStats/%s",n))
		Table tdat := XSplit(dat,"\n")
		Int lvl = -100
		For line in each(tdat)
			If line!="" 
				Table ins = xsplit(line," ")
				Switch ins[0]
					Case "REM"
						// DO NOTHING!
					Case "LEVEL"
						lvl = lua.tonumber(ins[1])
					Default
						If prefixed(ins[0],"STAT.") 
							If lvl==level
								String st := right(ins[0],len(ins[0])-5)
								RPGChar[Char].Stat["BASE_"..st] = Lua.tonumber(ins[1])
								Print("\t= "..st.." for "..Char.." is "..ins[1])
							End
						Else
							Lua.error("Unknown instruction! >> "..line)
						End
				End
			End
		End
		RPGChar[Char].Points.HP.Have = RPGChar[Char].Points.HP.Maximum
		If Char=="Jake_Human" && (!noauto)
			GrabLevel("Jake_Fairy",level,true)
		ElseIf Char=="Jake_Fairy" && (!noauto)
			GrabLevel("Jake_Human",level,true)
		End
	End
	
	Get Table TotalStat
		Static Var Ret
		If !Ret
			Ret = {}
			CSay("Creating Total Stat")
			For pref,tab in sPairs({[""]=Stats,["ZZ_ER_"]=Elements,["ZZ_SR_"]=Ailments})
				For s in Each(tab)
					Ret += Pref..s
				End
			End
		End
		Return Ret
	End
	
	Void _SetSkill(exp,lvl,int level)
		exp.Maximum = level * ({20,25,30})[__skill]
		lvl.Maximum = ({750,500,100})[__skill]
		If level>=lvl.Maximum 
			exp.Maximum = 0 
		End
		lvl.Have = level or 0
		lvl.Minimum = 0
	End
	
	Void SetSkill(string char,int i,int level)
		_SetSkill(RPGChar[char].Points["EXP_Skill"..i],RPGChar[char].Points["LVL_Skill"..i],Level)
	End
	
	Void CreateCharacter(string char, int level)
		Switch Char
			Case "Jake"
				CreateCharacter("Jake_Human",level)
				CreateCharacter("Jake_Fairy",level)
				For S in Each(TotalStat)
					For Sb in Each(SubStat)
						if Sb!="BASE" && Sb!="END"
							RPGChar.Jake_Fairy.Link_Stat("Jake_Human",sb.."_"..s)
						End
					End
				End
				RPGChar.Jake_Fairy.Link_Stat("Jake_Human","Level")
				RPGChar.Jake_Fairy.Link_Stat("Jake_Human","Experience")
				RPGChar.Jake_Fairy.Link_List("Jake_Human","*")
				RPGChar.Jake_Fairy.Link_Data("Jake_Human","*")
				RPGChar.Jake_Human.Name = "Jake"
				RPGChar.Jake_Human.Name = "Jake"
				For i=1,5
					RPGChar.Jake_Human.Points["EXP_Skill"..i].Have = 0 // must exist before linking, you know
					RPGChar.Jake_Human.Points["LVL_Skill"..i].Have = 0 // must exist before linking
					RPGChar.Jake_Fairy.Link_Points("Jake_Human","EXP_Skill"..i)
					RPGChar.Jake_Fairy.Link_Points("Jake_Human","LVL_Skill"..i)
				End
				Return
			Case "Krandar"
				RPGChar.Create("Krandar")
				Var Kr = RPGChar.Krandar
				Kr.Name = "Krandar"
				Kr.Stat.Level = 1234567
				Kr.Stat.Experience = 12345678
				For s,lc in pairs{["AP"]= "Marrilona", ["Accuracy"] = "Jake_Human", ["Critical"] =  "Jake_Human", ["Endurance"] =  "Dandor", ["Evasion"] =  "HandoStillor", ["HP"] =  "Dandor", ["Intelligence"] = "HandoStillor", ["Power"] = "Dandor", ["Resistance"] = "Marrilona", ["Speed"]="HandoStillor"}
					RPGChar.Krandar.Link_Stat(lc,"BASE_"..s)					
				End
				//Lua.error("No support for Krandar yet")
			Case "Nostramantu_Human" "Nostramantu"
				// Human!
				//Lua.error("No support for Nostramantu yet")
				//RPGChar.Create("Nostramantu_Human")
				//Var Nos = RPGChar.Nostramantu_Human
				RPGChar.Create(Char)
				Var Nos := RPGChar[Char]
				Nos.Name = "Nostramantu"
				Nos.Stat.Level = 1234567
				Nos.Stat.Experience = 12345678
				For s,lc in pairs{["AP"]= "Dandor", ["Accuracy"] = "Dandor", ["Critical"] =  "Marrilona", ["Endurance"] =  "Marrilona", ["Evasion"] =  "Dandor", ["HP"] =  "Marrilona", ["Intelligence"] = "HandoStillor", ["Power"] = "Marrilona", ["Resistance"] = "Marrilona", ["Speed"]="Dandor"}
					RPGChar[char].Link_Stat(lc,"BASE_"..s)					
				End	
			//Case "Nostramantu_Elder"
			//	Lua.error("No support for Elder Nostramantu yet")
			Case "Feenalaria_Human"
				// Human!
				RPGChar.Create("Feenalaria_Human")
				Var Kr = RPGChar.Feenalaria_Human
				Kr.Name = "Feenalaria"
				Kr.Stat.Level = 1234567
				Kr.Stat.Experience = 12345678
				For s,lc in pairs{["AP"]= "Marrilona", ["Accuracy"] = "Jake_Human", ["Critical"] =  "Jake_Human", ["Endurance"] =  "Jake_Human", ["Evasion"] =  "HandoStillor", ["HP"] =  "Dandor", ["Intelligence"] = "HandoStillor", ["Power"] = "Dandor", ["Resistance"] = "Marrilona", ["Speed"]="HandoStillor"}
					RPGChar.Feenalaria_Human.Link_Stat(lc,"BASE_"..s)					
				End
				//Lua.error("No support for Feenalaria yet")
			Case "Feenalaria_Human2"
				RPGChar.Create("Feenalaria_Human2")
				Var Kr = RPGChar.Feenalaria_Human2
				Kr.Name = "Feenalaria"
				//RPGChar.Feenalaria_Human2 = "Feenalaria_Human"
				RPGChar.Feenalaria_Human2.Link_Stat("Feenalaria_Human","*")
				RPGChar.Feenalaria_Human2.Link_List("Feenalaria_Human","*")
				RPGChar.Feenalaria_Human2.Link_Data("Feenalaria_Human","*")
				RPGChar.Feenalaria_Human2.Link_Points("Feenalaria_Human","*")
				Return
			Case "Feenalaria"
				RPGChar.Create("Feenalaria")
				RPGChar.Feenalaria.Name = "Feenalaria"
				Var Kr = RPGChar.Feenalaria
				Kr.Name = "Feenalaria"
				//RPGChar.Feenalaria_Human2 = "Feenalaria_Human"
				RPGChar.Feenalaria.Link_Stat("Feenalaria_Human","*")
				RPGChar.Feenalaria.Link_List("Feenalaria_Human","*")
				RPGChar.Feenalaria.Link_Data("Feenalaria_Human","*")
				RPGChar.Feenalaria.Link_Points("Feenalaria_Human","*")
				Return
			Default
				RPGChar.Create(char)
				RPGChar[char].Name=char
				RPGChar[char].Stat.Level = level
				RPGChar[char].Stat.Experience = EXP.ByLevel(Level+1)
				Grablevel(char,level,true) // Must be true in order to prevent conflicts between te two versions of Jake.
		End
		For S in Each(TotalStat)
			For Sb in Each(SubStat)
				If Sb!="BASE"
					RPGChar[char].Stat[sb.."_"..s] = 0
				End
				// Extra Bonus for Nostramantu on intelligence
				If Sb=="PERMABUFF" && Char=="Nostramantu" 
					Switch s
						case "Intelligence"
							RPGChar.Nostramantu.Stat.PERMABUFF_Intelligence = (RPGChar.Nostramantu.Stat.BASE_Intelligence*100) div __skill
						case "HP"
							RPGChar.Nostramantu.Stat.PERMABUFF_HP = RPGChar.Nostramantu.Stat.BASE_HP*100
						Case "AP" 
							RPGChar.Nostramantu.Stat.PERMABUFF_AP = RPGChar.Nostramantu.Stat.BASE_AP*(900 div __skill)
						default
							RPGChar.Nostramantu.Stat["PERMABUFF_"..s] = math.random(0,RPGChar.Nostramantu.Stat["BASE_"..s]*40)
					End		
				End
			End
			String Str:="SUM:"..char
			For SBS in Each(SuBStat)
				If SBS!="END"
					
					Str+="."..SBS.."_"..S
				End
			End
			RPGChar[char].StatScript["END_"..S]=Str
			//Print(S,Str,RPGChar[char].Stat["END_"..S])
		End
		For A in Each(Ailments)
			RPGChar[char].Stat["BASE_SR_"..A]= 100-math.floor(33*__skill)
		End
		RPGChar[char].Points.HP.MaxCopy  = "END_HP"
		RPGChar[char].Points.HP.Have     = RPGChar[char].Points.HP.Maximum
		RPGChar[char].Points.AP.MaxCopy  = "END_AP"
		RPGChar[char].Points.AP.Have     = RPGChar[char].Points.AP.Maximum
		RPGChar[char].Points.VIT.Maximum = ({250,100,0})[__skill]
		RPGChar[char].Points.VIT.Have    = RPGChar[char].Points.VIT.Maximum
		RPGChar[char].Data.EQUIP_Weapon  = ""
		RPGChar[char].Data.EQUIP_Armor   = ""
		RPGChar[char].Data.EQUIP_Jewel   = ""
		RPGChar[char].Data.Master        = ""
		For i=1,5
			Int val=0
			If i==1
				val=1
			End
			_SetSkill(RPGChar[char].Points["EXP_Skill"..i],RPGChar[char].Points["LVL_Skill"..i],val)
		End
		NeilUse("Script/Data/Char/"..char.."/Meta")
	End
	
	Get Int PartyCount
		If RPGParty[4]!=""
			Return 4
		End
		For i=1,3
			if RPGParty[i+1]==""
				Return i
			End
		End
		Return 0
	End
	
	Void CheckEquip(string Ch)
		// All
		If Ch=="*"
			For i=1,4
				If RPGParty[i] && RPGParty[i]!=""
					CheckEquip(RPGParty[i])
				End
			End
			Return
		End
		Var c := RPGChar[Ch]
		Var s := c.Stats
		// Reset to 0
		For stag In Each(Stats)
			s["EQUIP_"..stag] = 0
		End
		For etag in Each(Elements)
			s["EQUIP_ZZ_ER_"..etag] = 0
		End
		For atag in Each(Ailments)
			s["EQUIP_ZZ_SR_"..atag] = 0
		End
		// Now let's get ready to rumble
		For EQS in Each{"Weapon","Armor","Jewel"}
			String EQI := Trim(C.Data["EQUIP_"..EQS]:upper())
			CSayF("%s>%s -> '%s'",Ch,EQS,EQI)
			If EQI!=""
				Var I = Item[EQI]
				For stag In Each(Stats)
					If I.hasfield("EQP_Stat_"..stag)
						s["EQUIP_"..stag] += I["EQP_Stat_"..stag]
					End
				End
				For stag In Each(Elements)
					s["EQUIP_ZZ_ER_"..stag] += I["EQP_Stat_ER_"..stag]
				End
				For stag In Each(Ailments)
					s["EQUIP_ZZ_SR_"..stag] += I["EQP_Stat_SR_"..stag]
				End
			End
		End
	End
	
	Void Portrait(string ch,int X, int Y)
		Graphics.Scale(1,1)
		Switch ch
			Case "Feenalaria_Human"
				Pictures[ch] = Pictures[ch] || Image.Load("GFX/BoxText/Portret/Feenalaria/Human.png")
			Case "Feenalaria_Human2"
				Pictures[ch] = Pictures[ch] || Image.Load("GFX/BoxText/Portret/Feenalaria/Human2.png")
			Case "Nostramantu_Human"
				Pictures[ch] = Pictures[ch] || Image.Load("GFX/BoxText/Portret/Nostramantu/Human.png")
			Default
				Pictures[ch] = Pictures[ch] || Image.Load("GFX/BoxText/Portret/"..ch.."/General.png")
		End
		Pictures[ch].Hot(0,Pictures[ch].H)
		Pictures[ch].Draw(X,Y)
	End
	
	Table LvScale = {}
	Var ImgLevelUp = Image.Load(  "GFX/Party/Level Up.png","LevelUp")
	Var AudLevelUp = Audio.Load("Audio/Party/Level Up.ogg","LevelUp")


	Void Boxes(string HLChar="")
		Ach.Show()
		Static Table Boxes = { New CBox(0,__BoxPosY,20,128,false),New CBox(0,__BoxPosY,20,128,false),New CBox(0,__BoxPosY,20,128,false),New CBox(0,__BoxPosY,20,128,false)}
		Int C  = PartyCount
		Int M3 = 0
		Int W = Screen.W Div C
		Int Hover=0		
		Graphics.Scale(1,1)
		If C==3
			M3 = Screen.W % 3
		End
		Lua.assert(C>0,"Empty party")
		For i=1,C
			String Ch = RPGParty[i]
			Int W2 = W
			if i==3
				W2+=M3
			End
			LvScale[Ch] = LvScale[Ch] || 0
			// Background Box
			Boxes[i].X = (W*(i-1))
			Boxes[i].Y = __BoxPosY
			Boxes[i].W = W2
			Boxes[i].H = 100
			Boxes[i].Draw()
			If Ch=="Marrilona" && gv.gBool.MarFeena_Curse
				If RPGChar.Marrilona.Points.HP.Have>gv.gInt.MarFeena_HP
					RPGChar.Marrilona.Points.HP.Have--
				End
				if __skill<3 && gv.gInt.MarFeena_HP>2 && !Done[sprintf("Cursed_Marrilona_Been_in_%s:%s",Map.LoadedMap,Map.Layer)]
					gv.gInt.MarFeena_HP += math.random(0,10 div __skill)
					CSayF("Marrilona temp HP cap is now %04d",gv.gInt.MarFeena_HP)
				end
				gv.gBool.MarFeena_Curse = RPGChar.Marrilona.Points.HP.Maximum>gv.gInt.MarFeena_HP
			End
			if LvScale[Ch]>0
				LvScale[Ch] = (LvScale[Ch]+1) % 2500
				Graphics.SetScale(math.min(LvScale[Ch] / 500,1))
				ImgLevelUp.Draw(Boxes[i].X+(Boxes[i].W div 2),Boxes[i].Y-5)
			End
			If RPGChar[Ch].Stat.Level<__LevelCap && RPGChar[Ch].Stat.Experience<=0
				ImgLevelUp.HotBottomCenter()
				AudLevelUp.Play()
				LvScale[Ch]=1
				RPGChar[Ch].Stat.Level++
				Grablevel(Ch,RPGChar[Ch].Stat.Level)
				RPGChar[ch].Stat.Experience += EXP.ByLevel(RPGChar[Ch].Stat.Level)
				RPGChar[ch].Points.HP.Have     = RPGChar[ch].Points.HP.Maximum
				RPGChar[ch].Points.VIT.Have    = RPGChar[ch].Points.VIT.Maximum
			End
			If Mouse.MX>Boxes[i].X && Mouse.MY>Boxes[i].Y && Mouse.MX<Boxes[i].X+Boxes[i].W && Mouse.MY<Boxes[i].Y+Boxes[i].H
				Hover=i
				__Ember
				Graphics.SetAlpha(50)
				Graphics.Rect(Boxes[i].X,Boxes[i].Y,Boxes[i].W,Boxes[i].H)
				Graphics.SetAlpha(255)
			End
			Var B=Boxes[i]
			// Portrait
			__White
				If ch==HLChar
					plua alf = math.abs(math.floor(xmath.sin(FPS.Ticks/50)*255))
					//CSayF("Alpha: %03d / Tricks: %05d",alf,FPS.Ticks)
					Graphics.SetAlpha(alf)
				end
			Portrait(ch,B.X,Screen.H)
			Graphics.SetAlpha(255)
			// Bars			
			Int barx = B.X + 100
			Int barwidth = B.W-110			
			// HP
			Do
				Var HP = RPGChar[ch].Points.HP
				Number Breuk = HP.Have / HP.Maximum
				Int HPG = math.floor(255*Breuk)
				Int HPR = math.floor(255 - HPG)
				Int HBR = Math.floor(barwidth*Breuk)
				// print(HPR,HPG,0)
				Graphics.Color(50,50,50)
				Graphics.Rect(barx,B.Y+25,barwidth,10)
				Graphics.Color(HPR,HPG,0)
				Graphics.Rect(barx,B.Y+25,HBR,10)
				Fnt.DarkText("HP",barx,B.Y+35,1,1)
				Fnt.DarkText(HP.Have,barx+barwidth,B.Y+35,1,1)
			End
			// AP
			If RPGChar[ch].Points.AP.Maximum>0
				Var AP = RPGChar[ch].Points.AP
				Number Breuk = AP.Have / AP.Maximum
				Int APG = math.floor(180*Breuk)			
				Int ABB = 255
				Int ABR = Math.floor(barwidth*Breuk)
				// print(APR,APG,0)
				Graphics.Color(50,50,50)
				Graphics.Rect(barx,B.Y+50,barwidth,10)
				Graphics.Color(0,APG,ABB)
				Graphics.Rect(barx,B.Y+50,ABR,10)
				Fnt.DarkText("AP",barx,B.Y+60,1,1)
				Fnt.DarkText(AP.Have,barx+barwidth,B.Y+60,1,1)
				If gv.gbool.Cheat_Merlin
					AP.Have++
				End
			End
			// Vitality (if appliccable)
			If __Skill<3
				Var VIT = RPGChar[ch].Points.VIT
				Number Breuk = VIT.Have / VIT.Maximum
				Int VBR = Math.floor(barwidth*Breuk)
				// print(VITR,VITG,0)
				Graphics.Color(50,50,50)
				Graphics.Rect(barx,B.Y+75,barwidth,10)
				Graphics.Color(255,180,0)
				Graphics.Rect(barx,B.Y+75,VBR,10)
				Fnt.DarkText("VIT",barx,B.Y+85,1,1)
				Fnt.DarkText(VIT.Have,barx+barwidth,B.Y+85,1,1)
			End
			// Level (if appliccable)			
			If RPGChar[ch].Stat.Level<=__LevelCap
				__White
				Fnt.DarkText("Lv.",B.X,B.Y+B.H,0,1)
				__Ember
				BFnt.DarkText(Lua.tostring(RPGChar[ch].Stat.Level),B.X+25,B.Y+B.H,0,1)
			End
		End
		If Mouse.ML && Hover>0
			State.Call(Flow.Current,"PartyClick",sprintf("%d,\"%s\"",Hover,RPGParty[Hover]))
		End
	End
	
End
