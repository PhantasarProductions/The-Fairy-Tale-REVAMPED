// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/PRO_Town_Frendor.neil
// (c) Jeroen Broks, 2021, 2022, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 22.01.23
// End License Information

#use "Script/Use/Misc/Prologue"
#use "Script/Use/Misc/ManaOrb"
#use "Script/Use/Anyway"
#use "Script/Use/Misc/Inn"

Group MScript

	ReadOnly Bool HasDraw   = False
	ReadOnly Bool HasUpdate = False
	
	#region Prologue Start
	Void StartPrologue()
		MapText("JARIG")
	End
	
	Void NPC_YanneeInside()
		If Done.Prologue_Marrilona_SpokenToYanee
			MapText("YANNEE_PROLOGUE2")
		Else
			MapText("YANNEE_PROLOGUE1")
		End
	End
	
	Void ExitHouse()
		//If Done.Prologue_Marrilona_SpokenToYanee
		if __Chapter>=2
			Map.GoToLayer("TOWN","exitfrom_marrilona")
			Self.ComeInEvents()
		Else
			MapText("FANDALORA_PROLOGUE")
			Map.GoToLayer("ELDER","Marrilona")
			MapText("ELDER_PROLOGUE")
			Map.GoToMap("PRO_DUNGEON_MAGIC","#001","Start")
		End
	End	
	
	Void EnterMarrilona()
		Map.GoToLayer("MARRILONA","Entry")
		If !Done.DandorJoined
			Map.Kill("NPC_YanneeInside",true)
			RPGParty[3]="Dandor"
			RPGParty[4]="HandoStillor"
			Map.Pop("Dandor_","North",true)
			Map.SetObj("PLAYER3","Texture","GFX/Actors/Bundled/Player/Dandor.South.jpbf")
			RPGChar.Dandor.Points.LVL_Skill2.Have = 1
			RPGChar.Dandor.Points.LVL_Skill3.Have = 1
			MapText("FANDALORA_DANDORCURE2")
			Item.ZZKEY_WIND.Has = 1
			WorldMap.Unlock("CH2WINDSPIRATA")
		End
	End
	#endregion
	
	#region Fandalora
	
	Void Master_Fandalora()
		int need = math.floor(4.3 * __skill)
		int jake = 0
		int marrilona = 0
		for i=1,5
			jake += RPGChar.Jake_Human.Points["LVL_Skill"..i].Have 
			//if i>1 and RPG.PointsExists("Jake_Human","SK_LVL_"..i)==1 then jake      = jake      + RPG.Points("Jake_Human","SK_LVL_"..i).Have end
			marrilona += RPGChar.Marrilona.Points["LVL_Skill"..i].Have 
			//   if  RPG.PointsExists("Marrilona" ,"SK_LVL_"..i)==1 then marrilona = marrilona + RPG.Points("Marrilona","SK_LVL_"..i).Have end
		end
		int amarrilona = marrilona div 5
		int ajake      = jake      div 4
		CSay("Fandalora requires: "..need)
		CSay("Marrilona has:      "..amarrilona)
		CSay("Jake has:           "..ajake)
		if !gv.gbool["MASTER.FANDALORA"]
			MapText("FANDALORAMASTERNOMASTERYET")
			if need>amarrilona && need>ajake 
				MapText("FANDALORAMASTERNOTREADY") 
				return 
			end
		end
  		Master.Speak("Fandalora")
  	End
  	
	Void NPC_Fandalora()
		// Prologue
		If !Prologue.Done.Marrilona
			MapText("FANDALORA_PROLOGUE")
			Return
		End
		// Dandor
		// Master
		Master_Fandalora() // Lua.error("Master Fandalora not yet scripted")
	End
	
	Void NPC_FandaloraOutside()
		MapText("FANDALORA_OUTSIDE")
		Master_Fandalora()
	End
	
	Void NPC_YanneeOutside()
		If !gv.Bool.Spirata_Fire
			MapText("YANNEE_DANDORCURSED")
		Elseif !gv.Bool.DandorJoined
			MapText("YANNEE_DANDORCURED")
		Else
			MapText("YANNEE_OUTSIDE")
		End
	End
	#endregion
	
	
	
	#region Mana Orb
	Void ElderManaOrb()
		MapText("ELDER_MANAORB")
		ManaOrb()
		Schedule.Add("OrbChosen")
	End
	
	Void OrbChosen()
		//String Elem = ({"","Fire","Wind","Water","Earth"})[gv.gint.ManaOrb_LastChosen]
		MapText("ELDER_OVER")
		MapText("MARRILONA_OVER"..gv.gint.ManaOrb_LastChosen)
		MapText("ELDER_WARNING")
	End
	
	Void Elder_Exit()
		MapText("ELDER_WARNING")
		Combat.Boss("Hungry Fellow","Dwarf")
		Combat.Arena = Lua.Neil.Globals.Map.Arena
		Combat.Music = "*BOSS"
		Combat.Lose = "GameOver"
		Combat.NewEnemy = "Boss/Dwarf"
		Combat.StartEvent = "StartBoss"
		Lua.Neil.Globals.Map.StopWalking()
		Combat.Start()
		Schedule.Add("PostBoss")
	End
	
	Void PostBoss()
		Music.Stop()
		Map.GoToLayer("BLACK","Start")
		MapText("ELDER_OPENEYES")
		Map.GoToLayer("MARRILONA","MarrilonaBed")
		Map.ShowObj("PRE_NOS")
		MapText("ELDER_ENDPROLOGUE")
		Map.GoToMap("PRO_TheEnd","MARRILONA","Start")
	End
	
	Void StartBoss()
		MapText("STARTDWARFBOSS","FLOW_COMBAT")
		//RPGChar.FOE001.Points.HP.Minimum = RPGChar.FOE001.Points.HP.Maximum
		RPGChar.FOE001.Points.HP.Minimum = 1 
	End		
	#endregion
	
	#region Dandor
	Void DandorCaught()
		Map.Pop("Dan_","North",true)
		MapText("FRENDORDANDOR")
	End
	#endregion
	
	
	Void ComeInEvents()
		If !Done.Frendor_MarrillonaBrandedATraitorByNostramantu
			Map.HideObj("NPC_FandaloraOutside")
			MapText("TREASON")
			Map.Kill("ELDER_OUTSIDE",true)
			Map.GoToLayer("HARRY","Spot")
			Map.HideObj("Harry")
			Map.Pop("Harry","West")
			MapText("HARRY1")
			Map.ShowObj("Harry")
			MapText("HARRY2")
			Combat.Boss("Annoying Warrior Wannabe","Harry")
			Combat.Arena = Lua.Neil.Globals.Map.Arena
			Combat.Music = "Combat/SpecialBoss/AnnoyingBoy.ogg"
			Combat.Lose = "GameOver"
			Combat.NewEnemy = "Boss/Harry1"
			Lua.Neil.Globals.Map.StopWalking()
			Combat.Start()
			Schedule.Add("PostHarry")
			Return
		End
		// Library_Boss_Defeated
		If !Done.Elder_Went_For_Elven_Lands
			Map.HideObj("NPC_FandaloraOutside")
			MapText("PLEASEGO")
			WorldMap.UnLock("CH2ELFROAD")
			WorldMap.Lock("CH1FRENDOR")
			WorldMap.Go()
			__WorldMap = "ELDERGONE"
			Map.Kill("FANDALORA_ENTRANCE",true)
			Return
			//Lua.error("Elder going to the Elfen Lands not yet scripted")
		End
		If gv.Bool.Spirata_Fire
			Map.Kill("NPC_FandaloraOutside")
		Else
			Map.Kill("EnterMarrilona",false)
		End
		//Lua.error("Current entrance allowance not yet scripted")
	End
	
	Void PostHarry()
		MapText("HARRYPOST1")
		Map.HideObj("Harry")
		MapText("HARRYPOST2")
		WorldMap.Lock("CH1FRENDOR")
		WorldMap.UnLock("CH1HELL")
		WorldMap.Go()
		__WorldMap="HELL"
	End
	
	#region Buildings
	Void EnterBuilding(string L)
		Map.GoToLayer(L,"Start")
	End
	
	Void B_Exit()
		Map.GoToLayer("TOWN","exit_"..(Map.Layer:lower()))
	End
	#endregion
	
	#region Services
	Void NPC_Inn()
		Inn()
	End
	
	Void NPC_Alberta()
		MapText("ALBERTA")
		FieldMenu.Shop("ITEM_ALBERTA")
	End
	
	Void NPC_Michiel()
		MapText("MICHIEL")
		FieldMenu.WeaponShop("WEAPON_MICHIEL")
	End
	
	Void NPC_Wand()
		MapText("WAND")
		FieldMenu.WeaponShop("WAND_FRENDOR")
	End
	#endregion
		
	Void ByeBye()
		WorldMap.Unlock("CH1FRENDOR") // Can only be triggered once you brought Dandor here! 
		WorldMap.Go()
	End
	
	Constructor
		ZA.TabEnter{"ExitHouse","Elder_Exit","ComeInEvents","B_Exit", "ByeBye","Byebye","EnterMarrilona" }
		For L in Each {"wands","weapons","items","inn"}
			ZA.Enter("enter_"..L,"EnterBuilding",l:upper())
		End
	End
End