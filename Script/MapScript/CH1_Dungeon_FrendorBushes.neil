// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/CH1_Dungeon_FrendorBushes.neil
// (c) Jeroen Broks, 2021, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 21.12.14
// End License Information

#use "Script/Use/Anyway"

#macro __BossDone gv.gbool.Done_Chapter1_FrendorBushes_Boss

Group MScript

	ReadOnly Bool HasDraw   = False
	ReadOnly Bool HasUpdate = False
	
	Void StartChapter1()
		Switch RPGParty[1]
			Case "Jake_Human"
				MapText("START_JAKE")
			Case "Marrilona"
				MapText("START_MARRILONA")
			Default
				Lua.error("Can't handle: "..RPGParty[1])
		End
		//Map.Chapter(1)
		__LevelCap = CapUp()
		Respawn.Reset()
	End
	
	Void RemoveStartCrap()
		For OTag in Each{"PRC_Jake","PRC_Marrilona","Boss_Jake","Boss_Marrilona","BlockJake","BlockMarrilona","RemoveSure"}
			CSayF("Killing off object '%s' permanently!",OTag)
			Map.Kill(OTag,true)
		End
	End
		
	#region Boss Marrilona
	Void Boss_Marrilona()
		MapText("BOSS_MARRILONA")
		Combat.Boss("Another","Dwarf")
		Combat.Arena = Lua.Neil.Globals.Map.Arena
		Combat.Music = "*BOSS"
		Combat.Lose = "GameOver"
		Combat.NewEnemy = "Boss/Dwarf"
		Combat.StartEvent = "StartBoss_Marrilona"
		Lua.Neil.Globals.Map.StopWalking()
		Combat.Start()
		Schedule.Add("PostBoss_Marrilona")
	End
	
	Void StartBoss_Marrilona()
		MapText("STARTBOSS_MARRILONA","FLOW_COMBAT")		
		RPGChar.FOE001.Points.HP.Minimum = 1
	End
	
	Void PostBoss_Marrilona()
		RemoveStartCrap()
		Map.GoToMap("CH1_HUB_JAKEHUT","BLACK","Yeah")
		Schedule.Add("Start_Marrilona")
		__BossDone = True
	End
	#endregion
	
	#region Boss Jake
	Void Boss_Jake()
		MapText("BOSS_JAKE")
		Combat.QuickBoss("Berserk Fellow","Dwarf","Dwarf_J")
		Schedule.Add("PostBoss_Jake")
	End
	
	Void PostBoss_Jake()
		RemoveStartCrap()
		Map.GoToLayer("FRENDORBUSHES","PostBossJake")
		Map.ShowObj("DeadMarrilona")
		MapText("POSTBOSS_JAKE")
        Map.GoToMap("CH1_HUB_JAKEHUT","INTERIOR","Start")
		Schedule.Add("Start_Jake")
		__BossDone = True
	End
	#endregion
	
	
	Void Leave()
		If !__BossDone
			Switch RPGParty[1]
				Case "Jake_Human"
					MapText("DONTGO_JAKE")
				Case "Marrilona"
					MapText("DONTGO_MARRILONA")
				Default
					Lua.error("Can't handle: "..RPGParty[1])
			End
			Return
		End
		//Lua.error("Leaving not yet implemented")
		WorldMap.Go()
	End
	
	Void RemoveSure()
		CSay("Let's see! All cleared?")
		__BossDone = RPGParty[2]=="Marrilona"
		If __BossDone 
			CSay("Oops, stuff is still there?")
			RemoveStartCrap()
			CSay("Should be corrected now!")
		Else
			CSay("Boss not defeated, yet! Okay!")
		End
	End

	
	Constructor
		If !__NewGamePlus
			Map.Kill("NPC_Tai")
		End
		If !Done.Chapter1Announced
			Map.Chapter(1)		
		End
		ZA.TabEnter{"Boss_Jake","Boss_Marrilona","RemoveSure"}
		ZA.Enter("ExitSouthWest","Leave")
		ZA.Enter("ExitNorthEast","Leave")
	End
	
End