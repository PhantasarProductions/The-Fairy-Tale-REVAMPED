// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/CH3_Town_Nostraburg.neil
// (c) Jeroen Broks, 2022, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 22.04.08
// End License Information

#use "Script/Use/Anyway"
#use "Script/Use/Misc/Inn" 

#macro __DepriJake gv.gbool.DepriJake

Group MScript

	ReadOnly Bool HasDraw   = False
	ReadOnly Bool HasUpdate = False
	
	Const String SealedBoss                  = "Kabi"
	Const String SealedBoss_Name             = "Kabi"
	Const String SealedBoss_Head             = "World Destroyer"
	Const String SealedBoss_Krandar          = "Marrilona"
	
	Bool DontEnter = False
	
	#region Entering and leaving buildings
	Void Enter(string b)
		If __DepriJake
			MapText("GOTOKRANDAR")
			Return
		End

		If DontEnter
			DontEnter=False
		Else
			Map.GoToLayer(b:upper(),"Start")
		End
	End
	
	Void Exit()
		If Map.Layer!="items"
			DontEnter=True
		End
		Map.GoToLayer("TOWN","entry_"..Map.Layer:lower())
	End
	#endregion
	
	
	#region Krandar
	Void Master_Krandar()
		if !gv.gbool.Master_Krandar
			MapText("KRANDAR_PREMASTER")
			if gv.gint.SealsOpened<=0
				MapText("KRANDAR_NOTREADY")
				Return
			End
		end
		Master.Speak("Krandar")
	End
	Void NPC_Krandar()
		If !Done.Nostraburg_SpokenToKrandarTheFirstTime
			MapText("KRANDAR_FIRST")
			WorldMap.Unlock("CH3SPIRATAEARTH")
			__WorldMap = "TOEARTH" 
			Return
		End
		If gv.gbool.Spirata_Earth && (!Done.HandoStillorsSummoning)
			MapText("KRANDAR_SUMMONING_A")
			Graphics.CLS()
			Graphics.FLIP()
			Events.Poll()
			FPS.SLEEP(2500)
			MapText("KRANDAR_SUMMONING_B")
			Schedule.Add("StartSummoning")
			Return
		End
		Master_Krandar()
	End
	
	Void StartSummoning()
		Events.Flush()
		Map.GotoMap("CH3_Story_Dragon_Forest","SUMMON","Spot")		
		Schedule.Add("Summoning")
	End
	
	Void Summoning()
		Map.HideObj("PLAYER")
		MapText("SUMMONING")
		Map.GoToMap("CH3_Town_Nostraburg","TOWN","K_Jake")
		Schedule.Add("PostSummoning")
	End
	
	Void PostSummoning()
		Map.Pop("K_","North",true)
		MapText("KRANDAR_SUMMONING_C")
		WorldMap.Unlock("CH3PYRUS")
		__WorldMap = "PREDRAGON"
	End		
	#endregion
	
	#region Services
	Void NPC_Hotelmus()
		Inn()
	End
	
	Void NPC_Katinta()
		MapText("KATINTA")
		FieldMenu.Shop("ITEM_KATINTA")
	End
	
	Void NPC_Yllabonja()
		MapText("YLLABONJA")
		FieldMenu.WeaponShop("WAND_YLLABONJA")
	End
	
	Void NPC_Nathalia()
		MapText("NATHALIA")
		FieldMenu.WeaponShop("WEAPON_NATHALIA")
	End
	#endregion
	
	#region Blue Seal
	Void NPC_Calanus()
		MapText("CALANUS_"..Lua.tostring(Map.HasTag("NPC_BlueSeal")):upper())
	End
	#endregion
	
	Void Guard_Ramata()
		Map.Kill("Guard_Ramata",true)
		Map.StopWalking()
		MapText("RAMATA_GUARD")
	End
	
	Void Kokonora()
		If __DepriJake
			MapText("GOTOKRANDAR")
		Else
			WorldMap.Go("Kokonora")
		End
	End
		
	#region Depri Jake
	Void DepriJake()
		Map.Pop("Depri_","South",true)
		MapText("DEPRI_JAKE")
	End
	
	Void EnterKrandar()
		Map.GoToLayer("KRANDAR","Start")
	End
	
	Void JakeIsACoward()
		If !Done.Jake_Is_A_Coward
			Map.Pop("M_","North",true)
			MapText("JAKE_IS_A_COWARD")
			Respawn.Reset()
		End
	End
	
	Void AndWeAreOff()
		MapText("GOTOPYRUS")
		For i,ch in pairs{"Jake_Human","Krandar","Dandor","HandoStillor"}
			RPGParty[i] = ch
		End
		Map.GoToMap("CH4_Dungeon_PyrusPass","#000","Start")
	End
	
	Void CureMarrilona()
		// Act One
		Map.Pop("M_","North",true)
		MapText("CURE1")
		// Act Two
		Graphics.Cls()
		Graphics.Flip()
		Music.Play("Music/SYS/Silence.ogg")
		plua v={"DSB.ogg","Earth.ogg","Fire.ogg","Frost.ogg","Heal.ogg","Hit.ogg","Thunder.ogg","Water.ogg","Wind.ogg","Yell.ogg"}
		For vf in each(v)
			Events.Poll()
			if JCR6.EntryExists("Vocals/Combat/Foe/Marrilona/Action/"..vf)
				Audio.Load("Vocals/Combat/Foe/Marrilona/Action/"..vf,"BEAST_MARRILONA").Play()
				FPS.Sleep(1200) 
			end
		end
		Ach.Award("SCEN_SAVEMARRILONA")
		Map.Kill("Shroud",true)
		Map.SetObj("Marrilona","Texture","GFX/Actors/Bundled/Player/Marrilona.South.jpbf")
		Map.Remap()
		MapText("CURE2")
		// Act Three
		Player.WalkTo("Marrilona")
		MapText("CURE3")
		For i,p in pairs{"Jake_Human","Marrilona","Dandor","HandoStillor"}
			RPGParty[i] = p
			CSayF("%d> %s",i,p)
		End
		Music.Play("Music/Town/Funky Fairy.ogg")
		Map.GoToLayer("TOWN","K_Jake")
		Schedule.Add("FinalBriefing")
	End
	
	Void FinalBriefing()
		Respawn.Reset()
		Map.Pop("K_","North",true)
		MapText("CH4START")
	End
	#endregion
	
	Void NPC_Sinasina()
		// Note! This used to be a New Game+ dungeon only!
		// I however decided that it would be better to put this in the normal
		// playthrough now.
		MapText("SINASINA")
		WorldMap.Unlock("NGPDANDLETON")
	End
	
	Void NPC_Feenalaria()
		Static Var OtherMasters = { "Fandalora","Freya","Midas","Rubine","Russell","Krandar" }
		If !gv.gbool.FeenaRecruitedAsMaster
			MapText("FEENA_PREMASTER")
			Bool Good=True
			For gm in Each(OtherMasters)
				Good = Good && Master.Unlocked(gm)
				CSayF("%s ==> %s",gm,Lua.tostring(Good))
			End
			If !Good
				MapText("FEENA_NOTREADY")
				Return
			End
		End
		gv.gbool.FeenaRecruitedAsMaster = true
		Master.Speak("Feenalaria")
	End
	
	#region Nostramantu and Feenalaria
	Void Welcome_Nos()
		Map.Pop("Nos_","North",true)
		Map.HideByLabel("Huis")
		MapText("BYE_NOS")
		For i,ch in pairs{"Jake_Human","Marrilona","Dandor","HandoStillor"}
			RPGParty[i]=ch
		End
		Map.GoToMap("NGP_DUNGEON_DANDLETON","#007","Start")
		Schedule.Add("End007")
	End
	
	Void LovingAsshole()
    	MapText("FRENDOR_PREBOSS")
    	Combat.Prepare(255,102,0,"Loving and caring assho... I mean brother","Frendor")
    	Combat.NewEnemy="Boss/Frendor"
    	Combat.Music="Combat/Special Boss/Rush.ogg"
    	Combat.Arena="Forest"
    	Combat.Start()
    	//if     skill==1 then RPG.Points('Feenalaria','AP').Have=123456789
    	//elseif skill==2 then RPG.Points('Feenalaria','AP').Have=RPG.Points('Marrilona','AP').Have
    	//elseif skill==3 then RPG.Points('Feenalaria','AP').Have=0 end
    	Switch __skill
    		case 1
    			RPGChar.Feenalaria.Points.AP.Have=123456789
    		case 2
    			RPGChar.Feenalaria.Points.AP.Have=RPGChar.Marrilona.Points.AP.Have
    		case 3
    			RPGChar.Feenalaria.Points.AP.Have=0
    		default
    	End
    	RPGChar.Feenalaria.Points.VIT.Have=1000
    	RPGChar.Feenalaria.Points.HP.Have=RPGChar.Feenalaria.Points.HP.Maximum
    	Schedule.Add("AssholeDown")
    End
    
    Void AssholeDown()
	    //Party('Jake_Human;Marrilona;Dandor;HandoStillor')
	    For i,ch in pairs{"Jake_Human", "Marrilona", "Dandor", "HandoStillor"}
    		RPGParty[i] = ch
    	End
    	Map.GoToMap("NGP_Dungeon_Dandleton_Part2","#015","Einde")
    	Schedule.Add("ByeAsshole")
	End
	#endregion
	
	Constructor
		CSay("Welcome to Nostraburg")
		if Map.LoadedMap:upper()!="CH3_TOWN_NOSTRABURG"
			Return
		End
		If Prefixed(RPGParty[1],"Feena") || Prefixed(RPGParty[1],"Nostra")
			For t in Each{"NPC_Calanus","NPC_Krandar","NPC_Feenalaria","NPC_MT_Sylina","NPC_MT_Tero","NPC_MT_Wendy","NPC_MT_Cryogina","NPC_MT_Nova","NPC_MT_Ross","NPC_Sinasina","NPC_MT_Ramata"}
				Map.LayKill("TOWN",t,false)
			End
			If Prefixed(RPGParty[1],"Nostra")
				Map.LayKill("TOWN","Frendor",false)				
			End
		Else
			Map.LayKill("TOWN","Frendor",false)
			If __Chapter<4 || gv.gbool.DepriJake
				Map.LayKill("TOWN","NPC_Feenalaria",false)
				Map.LayKill("TOWN","NPC_Sinasina",false)
			End
			If gv.gbool.DepriJake
				Map.LayKill("TOWN","NPC_Krandar",false)
				ZA.Enter("ZA_ENTER_krandar","EnterKrandar")
				ZA.Enter("AndWeAreOff")
				Za.Enter("JakeIsACoward")
			End
			For z in Each{"weapons","inn","items","wands"}
				ZA.Enter("ZA_ENTER_"..z,"Enter",z)				
			End
			ZA.TabEnter{"Exit","Guard_Ramata","Kokonora"}
		End
		If __Chapter>=4
			Worldmap.Unlock("CH2VANDAR")
			WorldMap.Unlock("CH4MANAROAD")
		End
	End
End